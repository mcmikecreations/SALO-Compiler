їнасипати "converts.sut"

їякщо lang_sut == 1
їінакше
їнехай lang_sut 1
власний шматок реєструватистрічка
бере стрічка значення ₴ кінець
дає число ₴ кінець
смакує вернути 0 ₴ кінець
кінець реєструватистрічка

спільний шматок давайукр
бере
	нічого ₴
кінець
дає
	число ₴
кінець
смакує
	число а ₴
	число б ₴
	а = SetConsoleCP(1251) ₴
	б = SetConsoleOutputCP(1251) ₴
	якщо (а + б == 2)
	смакує
		вернути 0 ₴
	кінець якщо ₴
	вернути 1 ₴
кінець
кінець давайукр
спільний шматок ляпнути
бере
	стрічка вказівник ₴
кінець
дає
	число ₴
кінець
смакує
	вернути printstr(вказівник) ₴
кінець
кінець ляпнути

спільний шматок ляпнутичисло
бере
	число значення ₴
кінець
дає
	число ₴
кінець
смакує
	вернути printint32("%d", значення) ₴
кінець
кінець ляпнутичисло

спільний шматок ляпнутикуцик
бере
	куцик значення ₴
кінець
дає
	число ₴
кінець
смакує
	число ч ₴
	ч = куциквчисло(значення) ₴
	вернути printint32("%hd", ч) ₴
кінець
кінець ляпнутикуцик

спільний шматок ляпнутибуква
бере
	буква значення ₴
кінець
дає
	число ₴
кінець
смакує
	число ч ₴
	ч = буквавчисло(значення) ₴
	вернути printint32("%hhd", ч) ₴
кінець
кінець ляпнутибуква

спільний шматок ляпнутидріб
бере
	дріб значення ₴
кінець
дає
	число ₴
кінець
смакує
	//щоб провантажилась функція printf
	printstr("") ₴
	нічого_путь к ₴
	к = стрічкавпуть("%f") ₴
	рідний смакує
movss		xmm0,			dword ptr ebp+8
cvtss2sd	xmm0,			xmm0
  sub		esp,			8
movsd		qword ptr esp,	xmm0
 push		dword ptr ebp-4
 call		[printstr]
  add		esp,			12
	кінець рідний ₴
кінець
кінець ляпнутидріб

спільний шматок читатичисло
бере
	нічого ₴
кінець
дає
	число ₴
кінець
смакує
	число вказівник ₴
	вказівник = 0 ₴
	scanint32(" %d", &вказівник) ₴
	вернути вказівник ₴
кінець
кінець читатичисло
ївсеякщо
